using KrylovKit: linsolve

"""
    full_update(o::ITensor, ψ::TensorNetworkState, v⃗; envs, kwargs...)

Full update of two tensors in the presence of environments under the action of a two-site gate. More general than `simple_update` (allows for more accurate non-BP environments), but at a higher computational cost depending on the dimensions of the environment tensors involved.
"""
function full_update(
        o::ITensor,
        ψ::TensorNetworkState,
        v⃗;
        envs,
        nfullupdatesweeps = 10,
        print_fidelity_loss = false,
        envisposdef = false,
        callback = Returns(nothing),
        symmetrize = false,
        apply_kwargs...,
    )

    Qᵥ₁, Rᵥ₁ = factorize(
        ψ[v⃗[1]], uniqueinds(uniqueinds(ψ[v⃗[1]], ψ[v⃗[2]]), uniqueinds(ψ, v⃗[1]))
    )
    Qᵥ₂, Rᵥ₂ = factorize(
        ψ[v⃗[2]], uniqueinds(uniqueinds(ψ[v⃗[2]], ψ[v⃗[1]]), uniqueinds(ψ, v⃗[2]))
    )

    extended_envs = vcat(envs, Qᵥ₁, prime(dag(Qᵥ₁)), Qᵥ₂, prime(dag(Qᵥ₂)))
    Rᵥ₁, Rᵥ₂ = optimise_p_q(
        Rᵥ₁,
        Rᵥ₂,
        extended_envs,
        o;
        nfullupdatesweeps,
        print_fidelity_loss,
        envisposdef,
        apply_kwargs...,
    )
    if symmetrize
        M = Rᵥ₁ * Rᵥ₂
        codomain = inds(Rᵥ₁)
        # Balanced SVD: split the singular values symmetrically (√S into each factor).
        U, S, V = svd_trunc(M, codomain; trunc = itensor_trunc(; apply_kwargs...))
        u = only(commoninds(U, S))
        v = only(commoninds(S, V))
        sqrtS = sqrth_safe(S, (u,), (v,); atol = 0, rtol = 0)
        Rᵥ₁, Rᵥ₂ = U * replaceind(sqrtS, v, prime(u)), replaceind(sqrtS, u, prime(u)) * V
        # Best-effort truncation error from norms; suffers catastrophic cancellation when little is
        # discarded. TODO: expose MatrixAlgebraKit's `ϵ` from `ITensorBase.svd_trunc` and use it here.
        total = abs2(norm(M))
        truncation_error = iszero(total) ? zero(real(scalartype(M))) :
            max(zero(real(scalartype(M))), 1 - abs2(norm(S)) / total)
        callback(; singular_values = S, truncation_error)
    end
    ψᵥ₁ = Qᵥ₁ * Rᵥ₁
    ψᵥ₂ = Qᵥ₂ * Rᵥ₂
    return ITensor[ψᵥ₁, ψᵥ₂]
end

"""Calculate the overlap of the gate acting on the previous p and q versus the new p and q in the presence of environments. This is the cost function that optimise_p_q will minimise"""
function fidelity(
        envs::Vector{ITensor},
        p_cur::ITensor,
        q_cur::ITensor,
        p_prev::ITensor,
        q_prev::ITensor,
        gate::ITensor,
    )
    p_sind, q_sind = commonind(p_cur, gate), commonind(q_cur, gate)
    p_sind_sim, q_sind_sim = sim(p_sind), sim(q_sind)
    gate_sq =
        gate * replaceinds(dag(gate), Index[p_sind, q_sind], Index[p_sind_sim, q_sind_sim])
    term1_tns = vcat(
        [
            p_prev,
            q_prev,
            replaceind(prime(dag(p_prev)), prime(p_sind), p_sind_sim),
            replaceind(prime(dag(q_prev)), prime(q_sind), q_sind_sim),
            gate_sq,
        ],
        envs,
    )
    sequence = contraction_sequence(term1_tns; alg = "optimal")
    term1 = contract(term1_tns; sequence)

    term2_tns = vcat(
        [
            p_cur,
            q_cur,
            replaceind(prime(dag(p_cur)), prime(p_sind), p_sind),
            replaceind(prime(dag(q_cur)), prime(q_sind), q_sind),
        ],
        envs,
    )
    sequence = contraction_sequence(term2_tns; alg = "optimal")
    term2 = contract(term2_tns; sequence)
    term3_tns = vcat([p_prev, q_prev, prime(dag(p_cur)), prime(dag(q_cur)), gate], envs)
    sequence = contraction_sequence(term3_tns; alg = "optimal")
    term3 = contract(term3_tns; sequence)

    f = scalar(term3) / sqrt(scalar(term1) * scalar(term2))
    return f * conj(f)
end

"""Do Full Update Sweeping, Optimising the tensors p and q in the presence of the environments envs,
Specifically this functions find the p_cur and q_cur which optimise envs*gate*p*q*dag(prime(p_cur))*dag(prime(q_cur))"""
function optimise_p_q(
        p::ITensor,
        q::ITensor,
        envs::Vector{ITensor},
        o::ITensor;
        nfullupdatesweeps = 10,
        print_fidelity_loss = false,
        envisposdef = true,
        apply_kwargs...,
    )
    p_cur, q_cur = factorize(
        apply(o, p * q), inds(p); tags = tags(commonind(p, q)), apply_kwargs...
    )

    fstart = print_fidelity_loss ? fidelity(envs, p_cur, q_cur, p, q, o) : 0

    qs_ind = namesetdiff(inds(q_cur), collect(Iterators.flatten(inds.(vcat(envs, p_cur)))))
    ps_ind = namesetdiff(inds(p_cur), collect(Iterators.flatten(inds.(vcat(envs, q_cur)))))

    function b(p::ITensor, q::ITensor, o::ITensor, envs::Vector{ITensor}, r::ITensor)
        ts = vcat(ITensor[p, q, o, dag(prime(r))], envs)
        sequence = contraction_sequence(ts; alg = "optimal")
        return noprime(contract(ts; sequence))
    end

    function M_p(envs::Vector{ITensor}, p_q_tensor::ITensor, s_ind, apply_tensor::ITensor)
        ts = vcat(
            ITensor[
                p_q_tensor, replaceinds(prime(dag(p_q_tensor)), prime.(s_ind), s_ind), apply_tensor,
            ],
            envs,
        )
        sequence = contraction_sequence(ts; alg = "optimal")
        return noprime(contract(ts; sequence))
    end
    for i in 1:nfullupdatesweeps
        b_vec = b(p, q, o, envs, q_cur)
        M_p_partial = partial(M_p, envs, q_cur, qs_ind)

        p_cur, info = linsolve(
            M_p_partial, b_vec, p_cur; isposdef = envisposdef, ishermitian = false
        )

        b_tilde_vec = b(p, q, o, envs, p_cur)
        M_p_tilde_partial = partial(M_p, envs, p_cur, ps_ind)

        q_cur, info = linsolve(
            M_p_tilde_partial, b_tilde_vec, q_cur; isposdef = envisposdef, ishermitian = false
        )
    end

    fend = print_fidelity_loss ? fidelity(envs, p_cur, q_cur, p, q, o) : 0

    diff = real(fend - fstart)
    if print_fidelity_loss && diff < -eps(diff) && nfullupdatesweeps >= 1
        println(
            "Warning: Krylov Solver Didn't Find a Better Solution by Sweeping. Something might be amiss.",
        )
    end

    return p_cur, q_cur
end

partial = (f, a...; c...) -> (b...) -> f(a..., b...; c...)
