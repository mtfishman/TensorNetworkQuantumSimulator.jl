using Dictionaries: Dictionary, set!, delete!
using Graphs: AbstractGraph, is_tree, connected_components
using NamedGraphs.GraphsExtensions: default_root_vertex, forest_cover, post_order_dfs_edges, forest_cover_edge_sequence, boundary_edges, leaf_vertices, a_star
using LinearAlgebra: normalize

#TODO: Make this show() nicely.
struct BeliefPropagationCache{V, N <: AbstractTensorNetwork{V}, M <: Union{ITensor, Vector{ITensor}}} <:
    AbstractBeliefPropagationCache{V}
    network::N
    messages::Dictionary{NamedEdge, M}
    contraction_sequences::Dictionary{Pair, Vector}
    edge_sequence::Vector
end

function message_diff(message_a::ITensor, message_b::ITensor)
    n_a, n_b = norm(message_a), norm(message_b)
    f = abs2(dot(message_a, message_b) / (n_a * n_b))
    return 1 - f
end

messages(bp_cache::BeliefPropagationCache) = bp_cache.messages
network(bp_cache::BeliefPropagationCache) = bp_cache.network
graph(bp_cache::BeliefPropagationCache) = graph(network(bp_cache))

function BeliefPropagationCache(network, messages, contraction_sequences)
    return BeliefPropagationCache(network, messages, contraction_sequences, forest_cover_edge_sequence(graph(network)))
end
BeliefPropagationCache(network, messages) = BeliefPropagationCache(network, messages, Dictionary{Pair, Vector}())
BeliefPropagationCache(network) = BeliefPropagationCache(network, default_messages())

contraction_sequences(bp_cache::BeliefPropagationCache) = bp_cache.contraction_sequences

function Base.copy(bp_cache::BeliefPropagationCache)
    return BeliefPropagationCache(copy(network(bp_cache)), copy(messages(bp_cache)), copy(contraction_sequences(bp_cache)), copy(edge_sequence(bp_cache)))
end

default_bp_maxiter(g::AbstractGraph) = is_tree(g) ? 1 : _default_bp_update_maxiter

edge_sequence(bp_cache::BeliefPropagationCache) = bp_cache.edge_sequence

function set_edge_sequence(bp_cache::BeliefPropagationCache, edge_sequence::Vector)
    return BeliefPropagationCache(network(bp_cache), messages(bp_cache), contraction_sequences(bp_cache), edge_sequence)
end

function edge_scalar(bp_cache::BeliefPropagationCache, edge::AbstractEdge)
    return scalar(message(bp_cache, edge) * message(bp_cache, reverse(edge)))
end

#Algorithmic defaults
default_update_alg(bp_cache::BeliefPropagationCache) = "bp"
default_message_update_alg(bp_cache::BeliefPropagationCache) = "contract"
default_normalize(::Algorithm"contract") = true
default_sequence_alg(::Algorithm"contract") = "optimal"
function set_default_kwargs(alg::Algorithm"contract", bp_cache::AbstractBeliefPropagationCache)
    normalize = get(alg.kwargs, :normalize, default_normalize(alg))
    sequence_alg = get(alg.kwargs, :sequence_alg, default_sequence_alg(alg))
    return Algorithm("contract"; normalize, sequence_alg)
end
default_verbose(::Algorithm"bp") = false
default_tolerance(::Algorithm"bp") = nothing
function set_default_kwargs(alg::Algorithm"bp", bp_cache::BeliefPropagationCache)
    verbose = get(alg.kwargs, :verbose, default_verbose(alg))
    maxiter = get(alg.kwargs, :maxiter, default_bp_maxiter(bp_cache))
    _edge_sequence = get(alg.kwargs, :edge_sequence, edge_sequence(bp_cache))
    tolerance = get(alg.kwargs, :tolerance, default_tolerance(alg))
    message_update_alg = set_default_kwargs(
        get(alg.kwargs, :message_update_alg, Algorithm(default_message_update_alg(bp_cache))), bp_cache
    )
    return Algorithm("bp"; verbose, maxiter, edge_sequence = _edge_sequence, tolerance, message_update_alg)
end

function update_message!(
        message_update_alg::Algorithm, bp_cache::BeliefPropagationCache, edge::AbstractEdge
    )
    m, (cache_key, sequence, seq_changed) = updated_message(message_update_alg, bp_cache, edge)
    seq_changed && set!(contraction_sequences(bp_cache), cache_key, sequence)
    return setmessage!(bp_cache, edge, m)
end

function rescale_vertices!(
        bpc::BeliefPropagationCache,
        vertices::Vector
    )
    tn = network(bpc)

    for v in vertices
        vn = vertex_scalar(bpc, v)
        s = isreal(vn) ? sign(vn) : one(vn)
        if tn isa TensorNetworkState
            setindex_preserve!(tn, tn[v] * s * inv(sqrt(vn)), v)
        elseif tn isa TensorNetwork
            setindex_preserve!(tn, tn[v] * s * inv(vn), v)
        else
            error("Don't know how to rescale the vertices of this type")
        end
    end

    return bpc
end

const _default_bp_update_maxiter = 25
function default_tolerance(type)
    (type == Float32 || type == ComplexF32) && return 1.0e-5
    (type == Float64 || type == ComplexF64) && return 1.0e-8
    return nothing
end

function default_bp_update_kwargs(tn::AbstractTensorNetwork)
    if is_tree(tn)
        maxiter, tolerance, verbose = 1, nothing, false
    else
        maxiter, tolerance, verbose = _default_bp_update_maxiter, default_tolerance(scalartype(tn)), false
    end
    return (; maxiter, tolerance, verbose)
end

default_bp_update_kwargs(bp_cache::BeliefPropagationCache) = default_bp_update_kwargs(network(bp_cache))

function make_hermitian(A::ITensor)
    A_inds = inds(A)
    @assert length(A_inds) == 2
    return (A + replaceinds(conj(A), first(A_inds) => last(A_inds), last(A_inds) => first(A_inds))) / 2
end

function rescale_messages!(bp_cache::BeliefPropagationCache, edges::Vector{<:AbstractEdge})
    ms = messages(bp_cache)
    for e in edges
        me, mer = normalize(message(bp_cache, e)), normalize(message(bp_cache, reverse(e)))
        n = scalar(me * mer)
        if isreal(n)
            me *= sign(n)
            n *= sign(n)
        end
        set!(ms, e, me * inv(sqrt(n)))
        set!(ms, reverse(e), mer * inv(sqrt(n)))
    end
    return bp_cache
end

#Calculate the correlation flowing around single loop of the bp cache via an eigendecomposition
function loop_correlation(bpc::BeliefPropagationCache, loop::Vector{<:NamedEdge}, target_e::NamedEdge)

    is_tree(bpc) && return 0

    es = vcat(loop, [target_e])
    incoming_es = boundary_edges(bpc, es)
    incoming_messages = ITensor[message(bpc, e) for e in incoming_es]
    vs = unique(vcat(src.(loop), dst.(loop)))

    src_vertex = src(target_e)
    e_virtualinds = inds(message(bpc, target_e))
    e_virtualinds_sim = sim.(e_virtualinds)

    local_tensors = ITensor[]
    ts = bp_factors(bpc, src_vertex)

    for t in ts
        t_inds = filter(i -> i ∈ e_virtualinds, inds(t))
        if !isempty(t_inds)
            t_ind = only(t_inds)
            t_ind_pos = findfirst(x -> x == t_ind, e_virtualinds)
            t = replaceinds(t, t_ind => e_virtualinds_sim[t_ind_pos])
        end
        push!(local_tensors, t)
    end

    tensors = ITensor[local_tensors; reduce(vcat, [bp_factors(bpc, v) for v in setdiff(vs, [src_vertex])]); incoming_messages]
    seq = contraction_sequence(tensors; alg = "omeinsum", optimizer = GreedyMethod())
    t = contract_network(tensors; sequence = seq)

    t = matricize(t, e_virtualinds, e_virtualinds_sim)
    t = adapt(Vector{ComplexF64})(t)
    t = Array(t)
    λs = reverse(sort(LinearAlgebra.eigvals(t); by = abs))
    err = 1 - abs(λs[1]) / sum(abs.(λs))
    return err
end

#Calculate the correlations flowing around each of the primitive loops of the BP cache
function loop_correlations(bpc::BeliefPropagationCache, smallest_loop_size::Integer; kwargs...)
    g = graph(bpc)
    cycles = NamedGraphs.cycle_to_path.(NamedGraphs.unique_simplecycles_limited_length(g, smallest_loop_size))
    corrs = []
    for loop in cycles
        corrs = append!(corrs, loop_correlation(bpc, loop[1:(length(loop) - 1)], reverse(last(loop)); kwargs...))
    end
    return corrs
end

function loop_correlations(tn::AbstractTensorNetwork, smallest_loop_size::Integer; bp_update_kwargs = default_bp_update_kwargs(tn), kwargs...)
    return loop_correlations(update(BeliefPropagationCache(tn); bp_update_kwargs...), smallest_loop_size; kwargs...)
end
